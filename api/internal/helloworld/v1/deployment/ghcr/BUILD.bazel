load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_load", "oci_push")

# Put the cross compiled helloworld go_binary's into a tar layer.
tar(
    name = "helloworld_linux_arm64_tar",
    srcs = ["//api/internal/helloworld/v1/cmd/helloworld:helloworld_linux_arm64"],
    visibility = ["//visibility:private"],
)

oci_image(
    name = "helloworld_distroless_static_linux_arm64_v8",
    base = "@distroless_static_linux_arm64_v8",
    entrypoint = ["/api/internal/helloworld/v1/cmd/helloworld/helloworld_linux_arm64"],
    tars = [":helloworld_linux_arm64_tar"],
)

oci_image_index(
    name = "helloworld_image_index",
    images = [
        ":helloworld_distroless_static_linux_arm64_v8",
    ],
)

# SEE: https://github.com/bazel-contrib/bazel-lib/blob/fea951508738c95b4d18d93e444101f576458f60/lib/tests/expand_template/BUILD.bazel
# Use the value of --embed_label under --stamp, otherwise use a deterministic constant
# value to ensure cache hits for actions that depend on this.
expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    data = ["helloworld.tmpl"],
    stamp_substitutions = {
        "{{COMMIT}}": "{{STABLE_GIT_COMMIT}}",
    },
    template = "helloworld.tmpl",
)

# SEE: https://github.com/bazel-contrib/rules_oci/blob/843eb01b152b884fe731a3fb4431b738ad00ea60/docs/push.md
oci_push(
    name = "helloworld_push",
    image = ":helloworld_image_index",
    remote_tags = ":stamped",
    repository = "ghcr.io/fjarm/fjarm/helloworld",
)

oci_load(
    name = "helloworld_load",
    image = ":helloworld_distroless_static_linux_arm64_v8",
    repo_tags = ["fjarm/helloworld:local"],
)
